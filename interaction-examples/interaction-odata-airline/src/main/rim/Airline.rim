events
	GET GET
    POST POST
end

commands
	GETServiceDocument
	GETEntity
	GETEntities
	CreateEntity
	GETNavProperty
end

initial resource ServiceDocument
	item ServiceDocument
	view { GETServiceDocument }
	path "/"
	GET -> Airports
	GET -> Flights
	GET -> FlightSchedules
end


resource Airports
	collection Airport
	view { GETEntities }
	path "/Airports()"
	GET *-> airport id=code
	GET *-> departures id=code, navdepartures="departures"
	GET *-> arrivals id=code, navarrivals="arrivals"
end


resource airport
	item Airport
	view { GETEntity }
	path "/Airports('{id}')"
	GET -> departures id=code, navdepartures="departures"
	GET -> arrivals id=code, navarrivals="arrivals"
end



resource departures
	collection FlightSchedule
	view { GETNavProperty entity=Airport, navproperty=navdepartures }
	path "/Airports('{id}')/departures"
	GET -> FlightSchedules id=flightScheduleID
end

resource arrivals
	collection FlightSchedule
	view { GETNavProperty entity=Airport, navproperty=navarrivals }
	path "/Airports('{id}')/arrivals"
	GET -> FlightSchedules id=flightScheduleID
end

resource Flights
	collection Flight
	view { GETEntities }
	path "/Flights()"
	GET *-> flight id=flightID
	GET *-> flightschedule id=flightScheduleID
end


resource flight
	item Flight
	view { GETEntity }
	path "/Flights({id})"
	GET -> flightschedule id=flightScheduleID
end




resource FlightSchedules
	collection FlightSchedule
	view { GETEntities }
	path "/FlightSchedules()"
    POST -> FlightSchedules_pseudo_created
	GET *-> flightschedule id=flightScheduleID
	GET *-> departureAirport id=flightScheduleID, navdepartureAirport="departureAirport"
	GET *-> arrivalAirport id=flightScheduleID, navarrivalAirport="arrivalAirport"
end

resource FlightSchedules_pseudo_created
    item FlightSchedule
    view { GETEntity }
    actions { CreateEntity }
    path "/FlightSchedules()"   
end

resource flightschedule
	item FlightSchedule
	view { GETEntity }
	path "/FlightSchedules({id})"
	GET -> departureAirport id=flightScheduleID, navdepartureAirport="departureAirport"
	GET -> arrivalAirport id=flightScheduleID, navarrivalAirport="arrivalAirport"
end



resource departureAirport
	item Airport
	view { GETNavProperty entity=FlightSchedule, navproperty=navdepartureAirport }
	path "/FlightSchedules({id})/departureAirport"
	GET -> airport id=code
end

resource arrivalAirport
	item Airport
	view { GETNavProperty entity=FlightSchedule, navproperty=navarrivalAirport }
	path "/FlightSchedules({id})/arrivalAirport"
	GET -> airport id=code
end
