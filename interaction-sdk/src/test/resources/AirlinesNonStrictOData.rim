rim FlightResponder {

	event DELETE {
		method: DELETE
	}
	event GET {
		method: GET
	}
	event POST {
		method: POST
	}
	event PUT {
		method: PUT
	}

	command CreateEntity
	command DeleteEntity
	command GETEntities
	command GETEntity
	command GETException
	command GETNavProperty
	command GETServiceDocument
	command NoopGET
	command UpdateEntity

initial resource ServiceDocument {
	type: item
	entity: ServiceDocument
	view { GETServiceDocument }
	path "/"
	GET -> Flights
	GET -> Airports
	GET -> FlightSchedules
	GET -> Passengers
	GET -> ErrorMessages
}

exception resource InteractionException {
	type: item
	entity: InteractionException
	view { GETException }
}

resource ErrorMessages {
	type: collection
	entity: ErrorMessages
	view { NoopGET }
}


resource Flights {
	type: collection
	entity: Flight
	view { GETEntities filter=filter }
	path "/Flights()"
	POST -> Flights_created
	GET *-> flight id=flightID
	GET title="flightschedule" *-> flightschedule id=flightScheduleNum
}

resource flight {
	type: item
	entity: Flight
	view { GETEntity }
	path "/Flights({id})"
	GET title="flightschedule" -> flightschedule id=flightScheduleNum
}

resource Flights_created {
	type: item
	entity: Flight
	actions { CreateEntity }
	path "/Flights()"
	onerror --> ErrorMessages
}

resource Airports {
	type: collection
	entity: Airport
	view { GETEntities filter=filter }
	path "/Airports()"
	POST -> Airports_created
	GET *-> airport id=code
	GET title="departures" *-> FlightSchedules filter="departureAirportCode eq '{code}'"
	GET title="arrivals" *-> FlightSchedules filter="arrivalAirportCode eq '{code}'"
}

resource airport {
	type: item
	entity: Airport
	view { GETEntity }
	path "/Airports('{id}')"
	GET title="departures" -> FlightSchedules filter="departureAirportCode eq '{code}'"
	GET title="arrivals" -> FlightSchedules filter="arrivalAirportCode eq '{code}'"
}

resource Airports_created {
	type: item
	entity: Airport
	actions { CreateEntity }
	path "/Airports()"
}

resource FlightSchedules {
	type: collection
	entity: FlightSchedule
	view { GETEntities filter=filter }
	path "/FlightSchedules()"
	POST -> FlightSchedules_created
	GET *-> flightschedule id=flightScheduleID
	GET title="departureAirport" *-> airport id=departureAirportCode
	GET title="arrivalAirport" *-> airport id=arrivalAirportCode
}

resource flightschedule {
	type: item
	entity: FlightSchedule
	view { GETEntity }
	path "/FlightSchedules({id})"
	GET title="departureAirport" -> airport id=departureAirportCode
	GET title="arrivalAirport" -> airport id=arrivalAirportCode
}

resource FlightSchedules_created {
	type: item
	entity: FlightSchedule
	actions { CreateEntity }
	path "/FlightSchedules()"
}

resource Passengers {
	type: collection
	entity: Passenger
	view { GETEntities filter=filter }
	path "/Passengers()"
	POST -> Passengers_created
	GET *-> passenger id=passengerNo
	GET title="flight" *-> flight id=flightID
}

resource passenger {
	type: item
	entity: Passenger
	view { GETEntity }
	path "/Passengers({id})"
	GET title="flight" -> flight id=flightID
}

resource Passengers_created {
	type: item
	entity: Passenger
	actions { CreateEntity }
	path "/Passengers()"
}
}
