grammar com.temenos.interaction.rimdsl.RIMDsl with org.eclipse.xtext.common.Terminals

generate rim "http://www.temenos.com/interaction/rimdsl/RimDsl"

ResourceInteractionModel:
//     {Statemachine}
    ('events' 
        events+=Event+ 
    'end')?
    ('commands' 
        commands+=Command+ 
    'end')?
    states+=State*
;

Event:
    name=ID
;

State: 
    (isInitial?='initial' | isException?='exception')? 'resource' name=ID
        entity=Entity
        ('view' '{' view=[Command] '}')
        ('actions' '{' ((actions+=[Command]) (',' actions+=[Command])* ) '}')?
        path=Path?
        transitionsAuto+=TransitionAuto*
        transitions+=Transition*
        transitionsForEach+=TransitionForEach*
    'end'
;

Transition:
    (event=[Event] '->' state=[State] (eval=Expression)?) ((uriLinks+=UriLink)* (',' uriLinks+=UriLink)* )
;

TransitionForEach:
    (event=[Event] '*->' state=[State] (eval=Expression)?) ((uriLinks+=UriLink)* (',' uriLinks+=UriLink)* )
;

TransitionAuto:
    (event=[Event] '-->' state=[State] (eval=Expression)?)
;

Expression:
// works
//    '(' expression=ID ')'
//    '(' 'OK' '(' name=ID ')' ')'
//    '(' expression=Function_OK ')'
//    '(' expression=Function ')'

// does not work
//    '(' expression=[Function] ')'
    
    '(' 
        (expressions+=Function) 
        ('&&' (expressions+=Function))* 
    ')'
;

Function: Function_OK|Function_NOT_FOUND;

Function_OK:
    {OKFunction} 'OK' '(' state=[State] ')'
;

Function_NOT_FOUND:
    {NotFoundFunction} 'NOT_FOUND' '(' state=[State] ')'
;

Path:
    'path' name=STRING    
;

Entity:
    ((isCollection?='collection') | (isItem?='item'))? name=ID
;

Command:
    name=ID ((property+=CommandProperty)* (',' property+=CommandProperty)* )
;

CommandProperty:
    name=ID '=' value=ID
;

UriLink:
    (templateProperty=ID '=' entityProperty=UriLinkage)
;

UriLinkage:	UriLinkageEntityKeyReplace | UriLinkageProperty;

UriLinkageEntityKeyReplace:
	{UriLinkageEntityKeyReplace} name=ID
;

UriLinkageProperty:
	{UriLinkageProperty} name=STRING 
;