grammar com.temenos.interaction.rimdsl.RIMDsl with org.eclipse.xtext.common.Terminals

generate rim "http://www.temenos.com/interaction/rimdsl/RimDsl"


DomainModel:
    (rims += Ref)*
;

DomainDeclaration:
    'domain' name = QualifiedName '{'
        (rims += Ref)*
    '}'
;
 
Ref:
    DomainDeclaration | ResourceInteractionModel | Use
;

QualifiedName:
    ID ('.' ID)*
;

Use:
    'use' importedNamespace = QualifiedNameWithWildcard
;
  
QualifiedNameWithWildcard:
    QualifiedName '.*'?
;

ResourceInteractionModel:
    'rim' name = ID '{'
        (events += Event)*
        (commands += Command)*
        (states += State)*
    '}'
;

Event:
	'event' name=ID '{' 
     	'method:' httpMethod=ID
    '}'
;

Command:
	'command' name=ID ('{' 
     	'properties:' ((properties+=CommandProperty)* (',' properties+=CommandProperty)* )
    '}')?
;

CommandProperty:
    name=ID '=' (value=ID | value=STRING)
;

State: 
    (isInitial?='initial' | isException?='exception')? 'resource' name=ID '{'
    	type=ResourceType
        entity=Entity
        // it is only valid to have a view command or action commands.
        (('view' '{' view=ResourceCommand '}') | ('actions' '{' ((actions+=ResourceCommand) (';' actions+=ResourceCommand)* ) '}'))
        ('relations' '{' ((relations+=Relation) (',' relations+=Relation)* ) '}')?
        path=Path?
        transitions+=TransitionRef*
        ('onerror' '-->' errorState=[State])?
    '}'
;

// Order of transitions should be non deterministic
TransitionRef:
    Transition | TransitionForEach | TransitionAuto
;

// create regular transitions
Transition:
    (event=[Event] title=Title? '->' state=[State|QualifiedName] (eval=Expression)?) ((uriLinks+=UriLink)* (',' uriLinks+=UriLink)* )
;

// create foreach transitions
TransitionForEach:
    (event=[Event] title=Title? '*->' state=[State|QualifiedName] (eval=Expression)?) ((uriLinks+=UriLink)* (',' uriLinks+=UriLink)* )
;

// create AUTO transitions
TransitionAuto:
    (event=[Event] '-->' state=[State|QualifiedName] (eval=Expression)?) ((uriLinks+=UriLink)* (',' uriLinks+=UriLink)* )
;

Expression:
    '(' 
        (expressions+=Function) 
        ('&&' (expressions+=Function))* 
    ')'
;

Function: Function_OK|Function_NOT_FOUND;

Function_OK:
    {OKFunction} 'OK' '(' state=[State] ')'
;

Function_NOT_FOUND:
    {NotFoundFunction} 'NOT_FOUND' '(' state=[State] ')'
;

Relation:
    name=STRING    
;

Path:
    'path:' name=STRING    
;


ResourceType:
    'type:' ((isCollection?='collection') | (isItem?='item'))
;

Entity:
    'entity:' name=ID
;

ResourceCommand:
    (command=[Command] ((parameters+=CommandProperty)* (',' parameters+=CommandProperty)* ))
;

UriLink:
    (templateProperty=ID '=' entityProperty=UriLinkage)
;

UriLinkage:	UriLinkageEntityKeyReplace | UriLinkageProperty;

UriLinkageEntityKeyReplace:
	{UriLinkageEntityKeyReplace} name=ID
;

UriLinkageProperty:
	{UriLinkageProperty} name=STRING 
;

Title:
    'title' '=' name=STRING    
;
