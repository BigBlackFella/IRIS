grammar com.temenos.interaction.rimdsl.RIMDsl with org.eclipse.xtext.common.Terminals

generate rim "http://www.temenos.com/interaction/rimdsl/RimDsl"

ResourceInteractionModel:
//     {Statemachine}
    ('events' 
        events+=Event+ 
    'end')?
    ('commands' 
        commands+=Command+ 
    'end')?
    states+=State*
;

Event:
    name=ID
;

State:
    (isInitial?='initial')? 'resource' name=ID
        entity=Entity
        ('actions' '{' ((actions+=[Command]) (',' actions+=[Command])* ) '}')
        path=Path?
        urispec=UriSpecification?
        transitionsAuto+=TransitionAuto*
        transitions+=Transition*
        transitionsForEach+=TransitionForEach*
    'end'
;

Transition:
    (event=[Event] '->' state=[State] ((uriLinks+=UriLink)* (',' uriLinks+=UriLink)* ))
;

TransitionForEach:
    (event=[Event] '*->' state=[State] ((uriLinks+=UriLink)* (',' uriLinks+=UriLink)* ))
;

TransitionAuto:
    (event=[Event] '-->' state=[State])
;

Path:
    'path' name=STRING    
;

UriSpecification:
    'urispec' type=('NavProperty'|'EntitySet'|'Entity') pathPrefix=STRING
;

Entity:
    ((isCollection?='collection') | (isItem?='item'))? name=ID
;

Command:
    name=ID ((property+=CommandProperty)* (',' property+=CommandProperty)* )
;

CommandProperty:
    name=ID '=' value=ID
;

UriLink:
    templateProperty=ID '=' entityProperty=ID
;
